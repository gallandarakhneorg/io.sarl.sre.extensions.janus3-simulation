/*
 * $Id$
 *
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 *
 * Copyright (C) 2014-2019 the original authors or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package io.sarl.sre.extensions.simulation.services.time

import io.sarl.sre.extensions.simulation.boot.configs.SimulationConfig
import io.sarl.sre.extensions.simulation.boot.configs.TimeConfig
import io.sarl.sre.services.logging.LoggingService
import io.sarl.sre.services.time.AbstractTimeService
import java.text.MessageFormat
import java.util.concurrent.TimeUnit
import javax.inject.Singleton
import org.eclipse.xtend.lib.annotations.Accessors
import javax.inject.Inject
import io.sarl.sre.internal.SmartListenerCollection
import io.sarl.sre.services.time.TimeListener

/**
 * Time service based on a simulated time.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $GroupId$
 * @mavenartifactid $ArtifactId$
 * @since 0.10
 */
@Singleton
class SimulatedTimeService extends AbstractTimeService {

	var simulatedTime = 0.0

	var osTimeOfPreviousStep = 0.0

	var osFactor = 1.0

	@Accessors(PUBLIC_GETTER)
	val timeConfiguration : TimeConfig

	@Accessors(PUBLIC_GETTER)
	val loggingService : LoggingService

	/** Create a simulated time service.
	 * 
	 * @param loggingService the logging service.
	 * @param config the accessor to the simulation configuration.
	 * @param listeners the empty collection of listeners that must be used by this service. 
	 */
	@Inject
	new (loggingService : LoggingService, config : SimulationConfig, listeners : SmartListenerCollection<TimeListener>) {
		this(loggingService, config.time, listeners)
	}

	/** Create a simulated time service.
	 * 
	 * @param loggingService the logging service.
	 * @param config the accessor to the time configuration.
	 * @param listeners the empty collection of listeners that must be used by this service. 
	 */
	new (loggingService : LoggingService, config : TimeConfig, listeners : SmartListenerCollection<TimeListener>) {
		super(listeners)
		this.loggingService = loggingService
		this.timeConfiguration = config
		this.osTimeOfPreviousStep = this.OSCurrentTime
	}

	def getTime(timeUnit : TimeUnit) : double {
		 val tp = timePrecision
		 val tu = timeUnit ?: tp
		 if (tu === tp) {
		 	return this.simulatedTime
		 }
		 return this.simulatedTime.convertFromTo(tp, tu)
	}
	
	def getOSCurrentTime : long {
		System::currentTimeMillis
	}
	
	def getOSTimeFactor : double {
		 this.osFactor
	}

	def getTimePrecision : TimeUnit {
		this.timeConfiguration.unit
	}

	def evolveTimeIfPossible(timeDelta : double = 0.0) : boolean {
		if (timeDelta > 0.0) {
			this.simulatedTime += timeDelta
			var osdelta = this.OSCurrentTime - this.osTimeOfPreviousStep
			this.osFactor = osdelta / timeDelta
			if (this.timeConfiguration.timeProgressionInLogs) {
				this.loggingService.kernelLogger.info [
					MessageFormat::format(Messages::SimulatedTimeService_0, this.simulatedTime)
				]
			}
			fireTimeChanged
			return true
		}
		return false
	}
	
	def setTimeIfPossible(time : double) : boolean {
		if (time != this.simulatedTime) {
			this.simulatedTime = time
			fireTimeChanged
			return true
		}
		return false
	}
	
}
